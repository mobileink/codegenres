ISO/IEC 13568:2002(E) P. 25

7.2 Formal definition of context-free lexis

Copyright notice

The reproduction of this clause is permitted on the understanding that
this material is public domain, and on the condition that this
International Standard is referenced as the source document. With the
exception of clauses 6.2, 7.2, 8.2, 11 and annex B, all other parts of
the text are subject to the usual copyright rules stated on page ii of
this International Standard.



TOKENSTREAM = { SPACE } , { TOKEN , { SPACE } } ;

TOKEN = DECORWORD | NUMERAL | STROKE
| (-tok | )-tok | [-tok | ]-tok | f-tok | g-tok | hj | ji | hh | ii
| ZED | AX | GENAX | SCH | GENSCH | END | NL
;

DECORWORD = WORD , { STROKE } ;

WORD = WORDPART , { WORDPART }
| ( LETTER | ( DIGIT | DECIMAL ) ) , ALPHASTR , { WORDPART }
| SYMBOL , SYMBOLSTR , { WORDPART }
;

WORDPART = WORDGLUE , ( ALPHASTR | SYMBOLSTR ) ;

ALPHASTR = { LETTER | DIGIT } ;

SYMBOLSTR = { SYMBOL } ;

NUMERAL = NUMERAL , DECIMAL
| DECIMAL
;

STROKE = STROKECHAR
| '&' , DECIMAL , '-'
;

(-tok = '(' ;

)-tok = ')' ;

[-tok = '[' ;

]-tok = ']' ;

f-tok = 'f' ;

g-tok = 'g' ;

hj = 'hj' ;

ji = 'ji' ;

hh = 'hh' ;

ii = 'ii' ;

ZED = ZEDCHAR ;

AX = AXCHAR ;

SCH = SCHCHAR ;

GENAX = AXCHAR , GENCHAR ;

GENSCH = SCHCHAR , GENCHAR ;

END = ENDCHAR ;

NL = NLCHAR ;


